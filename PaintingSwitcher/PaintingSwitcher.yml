PaintingSwitcher:
    type: command
    debug: false
    name: PaintingSwitcher
    description: Toggles the ability to change paintings with the scroll wheel      
    usage: /PaintingSwitcher
    script:
        - if <player.has_flag[PaintingSwitchToggled]> == false || <player.flag[PaintingSwitchToggled]> == 0 {
            - flag <player> PaintingSwitchToggled:1
            - flag <player> PaintingList:|:Kebab|Aztec|Alban|Aztec2|Bomb|Plant|Wasteland|Wanderer|Graham|Pool|Courbet|Sunset|Sea|Creebet|Match|Bust|Stage|Void|Skull_And_Roses|Wither|Fighters|Skeleton|DonkeyKong|Pointer|Pigscene|BurningSkull
            - flag <player> PaintingCounter:1
            - flag <player> scrollCounter:0
            - narrate "<Gold> Painting Switcher Toggled ON"
        } else {
            - flag <player> PaintingSwitchToggled:!
            - flag <player> PaintingList:!
            - flag <player> PaintingCounter:!
            - flag <player> scrollCounter:!
            
            
            - narrate "<Gold> Painting Switcher Toggled OFF"
        }

PaintingSwitch:
    type: world
    debug: false
    events:
        on player scrolls their hotbar:
        # we first check if the player has the flag, so we can short circut the if statement when the player doesnt have the flag set.
        # This prevents spamming the console with invalid tag errors. We check for scroll counter mod 2 becuase when the scroll is canceled,
        # it seems to cause a second scroll event resulting in two paintings switching per scroll. 
        - if  <player.has_flag[PaintingSwitchToggled]> == true && <player.flag[PaintingSwitchToggled]> == 1 && <player.flag[scrollCounter].as_int.mod[2.0]> == 0 {

            - determine passively cancelled 
            
            - define nextPainting <player.flag[PaintingList].get[<player.flag[PaintingCounter].as_int>]>   
            
            - adjust <player.location.cursor_on.find.entities[painting].within[3].get[1]> painting:<def[nextPainting]>
        
            - if <player.flag[PaintingCounter].as_int> < <player.flag[PaintingList].size> {
                - flag <player> PaintingCounter:++
            } else {
                - flag <player> PaintingCounter:1
            } 
         }
        
        - flag <player> scrollCounter:++
